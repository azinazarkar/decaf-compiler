package compiler.Parser;
import java_cup.runtime.*;
import compiler.Scanner.MyScanner;
import compiler.CodeGenerator.SymbolTable.*;
import compiler.CodeGenerator.SymbolTable.Utility.*;


parser code {:
	private MyScanner scanner;
	public parser (MyScanner scanner){
		this.scanner = scanner;
	}
:}
scan with {: return scanner.next_token(); :}
// defining terminals here
// arithmetic operators
terminal        PLUS, MINUS, MULTIPLY, DIVIDE, MOD, UMINUS,
				LESS, LESSEQUAL, GREATER, GREATEREQUAL, EQUAL, NOTEQUAL;
// logical operators
terminal	    AND, OR, NOT;
// dot
terminal        DOT;
// types
terminal        String INT, DOUBLE, BOOL, STRING;
// signs
terminal		OPENBRACKET, CLOSEBRACKET, OPENCLOSEBRACKET, WS, OPENPARENTHESIS,
				CLOSEPARENTHESIS, OPENCURLYBRACES, CLOSECURLYBRACES;
// keywords
terminal        READINTEGER, READLINE, NEW, NEWARRAY, ITOD, DTOI, ITOB, BTOI,
				DECIMAL, FLOATINGPOINT, BOOLEANLITERAL, NULL,
				VOID, CLASS, INTERFACE,
				EXTENDS, IMPLEMENTS, COMMA, SEMICOLON, PRIVATE, PROTECTED, PUBLIC, IF, ELSE, WHILE, FOR, RETURN,
				BREAK, CONTINUE, PRINT, ASSIGN, THIS;
terminal        String IDENTIFIER, STRINGLITERAL;

// defining nonterminals
nonterminal     Program, DeclStar, Decl, VariableDecl, Variable, FunctionDecl, Formals, CommaVariables,
				ClassDecl, ClassDeclExtends, ClassDeclImplements, CommaIdentifiers, FieldStar, Field,
				AccessMode, InterfaceDecl, PrototypeStar, Prototype, StmtBlock, InsideStmtBlock, VariableDeclStar, StmtStar,
				Stmt, ExprPrime, IfStmt, ElsePrime, WhileStmt, ForStmt, ReturnStmt, BreakStmt, ContinueStmt, PrintStmt,
				CommaExpr, Expr, LValue, Call, Actuals, Constant;
// non-terminals with type
nonterminal     String Type;

// defining precedences
precedence left     ELSE;
precedence right    ASSIGN;
precedence left     OR;
precedence left     AND;
precedence left     EQUAL, NOTEQUAL;
precedence left     LESS, LESSEQUAL, GREATER, GREATEREQUAL;
precedence left     PLUS, MINUS;
precedence left     MULTIPLY, DIVIDE, MOD;
precedence right    NOT, UMINUS;
precedence left     DOT, OPENBRACKET;
precedence left     OPENPARENTHESIS;

start with Program;

Program ::=             Decl DeclStar;

DeclStar ::=            Decl DeclStar
						| /*epsilon*/
						;

Decl ::=                VariableDecl
						| FunctionDecl
						| ClassDecl
						| InterfaceDecl
						;

VariableDecl ::=        Variable SEMICOLON;

Variable ::=            Type:t IDENTIFIER:id
						{:
							System.out.println( "Identifier with type " + t + " and name " + id );
							SymbolTable.getInstance().getSymbolTable().addEntry(
								id,
								new Descriptor( Descriptor.getType( t ), null )
							);
						:}
						;

Type ::=                INT {: RESULT = "INT"; :}
						| DOUBLE {: RESULT = "DOUBLE"; :}
						| BOOL {: RESULT = "BOOL"; :}
						| STRING {: RESULT = "STRING"; :}
						| IDENTIFIER:e {: RESULT = e; :}
						| Type OPENCLOSEBRACKET {: RESULT = "ARRAY"; :}
						;

FunctionDecl ::=        Type:t IDENTIFIER:name OPENPARENTHESIS
						{:
							System.out.println( "Starting to get formals." );
							SymbolTable.getInstance().makeNextAndSwitch();
						:}
						Formals
						{:
							System.out.println( "After getting formals: " );
							System.out.println( SymbolTable.getInstance().getSymbolTable() );
							SymbolTable.getInstance().makeNextAndSwitch();
						:} CLOSEPARENTHESIS StmtBlock {:
							System.out.println( "A function!" );
							System.out.println( "\ttype: " + t );
							System.out.println( "\tname: " + name );
							System.out.println( SymbolTable.getInstance().getSymbolTable() );
							SymbolTable.getInstance().goBack();
							SymbolTable.getInstance().goBack();
						:}
						| VOID IDENTIFIER OPENPARENTHESIS Formals CLOSEPARENTHESIS StmtBlock
						;

Formals ::=             Variable:v CommaVariables
						| /*epsilon*/
						;
CommaVariables ::=      COMMA Variable CommaVariables
						| /*epsilon*/
						;

ClassDecl ::=           CLASS IDENTIFIER ClassDeclExtends ClassDeclImplements OPENCURLYBRACES FieldStar CLOSECURLYBRACES;
ClassDeclExtends ::=    EXTENDS IDENTIFIER
						| /*epsilon*/
						;
ClassDeclImplements ::= IMPLEMENTS IDENTIFIER CommaIdentifiers
						| /*epsilon*/
						;
CommaIdentifiers ::=    COMMA IDENTIFIER CommaIdentifiers
						| /*epsilon*/
						;
FieldStar ::=           Field FieldStar
						| /*epsilon*/
						;

Field ::=               AccessMode VariableDecl
						| AccessMode FunctionDecl
						;

AccessMode ::=          PRIVATE
						| PROTECTED
						| PUBLIC
						| /*epsilon*/
						;

InterfaceDecl ::=       INTERFACE IDENTIFIER OPENCURLYBRACES PrototypeStar CLOSECURLYBRACES;
PrototypeStar ::=       Prototype PrototypeStar
						| /*epsilon*/
						;

Prototype ::=           Type IDENTIFIER OPENPARENTHESIS Formals CLOSEPARENTHESIS SEMICOLON
						| VOID IDENTIFIER OPENPARENTHESIS Formals CLOSEPARENTHESIS SEMICOLON
						;

StmtBlock ::=           OPENCURLYBRACES InsideStmtBlock CLOSECURLYBRACES;
InsideStmtBlock ::=     VariableDecl InsideStmtBlock
						| StmtStar
						;
StmtStar ::=            Stmt StmtStar
						| /*epsilon*/
						;

Stmt ::=                ExprPrime SEMICOLON
						| IfStmt
						| WhileStmt
						| ForStmt
						| BreakStmt
						| ContinueStmt
						| ReturnStmt
						| PrintStmt
						| StmtBlock
						;

ExprPrime ::=           Expr
						| /*epsilon*/
						;

IfStmt ::=              IF OPENPARENTHESIS Expr CLOSEPARENTHESIS Stmt ElsePrime;
ElsePrime ::=           ELSE Stmt
						| /*epsilon*/
						;

WhileStmt ::=           WHILE OPENPARENTHESIS Expr CLOSEPARENTHESIS Stmt;

ForStmt ::=             FOR OPENPARENTHESIS ExprPrime SEMICOLON Expr SEMICOLON ExprPrime CLOSEPARENTHESIS Stmt;

ReturnStmt ::=          RETURN ExprPrime SEMICOLON;

BreakStmt ::=           BREAK SEMICOLON;

ContinueStmt ::=        CONTINUE SEMICOLON;

PrintStmt ::=           PRINT OPENPARENTHESIS Expr CommaExpr CLOSEPARENTHESIS SEMICOLON;

CommaExpr ::=           COMMA Expr CommaExpr
						| /*epsilon*/
						;

Expr ::=                LValue ASSIGN Expr
						| Constant
						| LValue
						| THIS
						| Call
						| OPENPARENTHESIS Expr CLOSEPARENTHESIS
						| Expr PLUS Expr
						| Expr MINUS Expr
						| Expr MULTIPLY Expr
						| Expr DIVIDE Expr
						| Expr MOD Expr
						| MINUS Expr %prec UMINUS
						| Expr LESS Expr
						| Expr LESSEQUAL Expr
						| Expr GREATER Expr
						| Expr GREATEREQUAL Expr
						| Expr EQUAL Expr
						| Expr NOTEQUAL Expr
						| Expr AND Expr
						| Expr OR Expr
						| NOT Expr
						| READINTEGER OPENPARENTHESIS CLOSEPARENTHESIS
						| READLINE OPENPARENTHESIS CLOSEPARENTHESIS
						| NEW IDENTIFIER
						| NEWARRAY OPENPARENTHESIS Expr COMMA Type CLOSEPARENTHESIS
						| ITOD OPENPARENTHESIS Expr CLOSEPARENTHESIS
						| DTOI OPENPARENTHESIS Expr CLOSEPARENTHESIS
						| ITOB OPENPARENTHESIS Expr CLOSEPARENTHESIS
						| BTOI OPENPARENTHESIS Expr CLOSEPARENTHESIS
						;

LValue ::=              IDENTIFIER
						| Expr DOT IDENTIFIER
						| Expr OPENBRACKET Expr CLOSEBRACKET
						;

Call ::=                IDENTIFIER OPENPARENTHESIS Actuals CLOSEPARENTHESIS
						| Expr DOT IDENTIFIER OPENPARENTHESIS Actuals CLOSEPARENTHESIS
						;

Actuals ::=             Expr CommaExpr
						| /*epsilon*/
						;

Constant ::=            DECIMAL
						| FLOATINGPOINT
						| BOOLEANLITERAL
						| STRINGLITERAL
						| NULL
						;

